// author: Valeria AÃ±orve-Garibay
// SLiM version 4.1
// No scaling factor

initialize() {
  	defineConstant("P_O", 0.0);
	initializeTreeSeq(checkCoalescence=T);
	initializeMutationRate(1e-08);
	// QTLs
	initializeMutationType("m1", 0.5, "n", 0.0, 0.25); // QTL
	m1.convertToSubstitution == F;
	initializeGenomicElementType("g1", c(m1), c(1));
	rec_ends = NULL;
	rec_rates = NULL;
	// for (index in 1:20){
	for (index in 1:20){
		initializeGenomicElement(g1, (index - 1 )*25000 + 2, (index - 1 )*25000 + 24999 + 2);
		rec_ends = c( rec_ends, (index - 1)*25000 + 2);
		rec_ends = c( rec_ends, (index - 1)*25000 + 24999 + 2);
		rec_rates = c( rec_rates, 0.5);
		rec_rates = c( rec_rates, 1e-08*1);
	}
	initializeRecombinationRate(rec_rates, rec_ends);
}

mutationEffect(m1) { return 1.0; }
// burn-in (10N)
1 early(){
	// save this run's identifier, used to save and restore
	defineConstant("simID", getSeed());
	sim.addSubpop("p1", 10000); }

1: late() {
	if (sim.treeSeqCoalesced())
	{
		catn(sim.cycle + ": COALESCED");
	}
}

1: late() {
	inds = p1.individuals;
	// sum the additive effect sizes of QTLs
	G = inds.sumOfMutationsOfType(m1);
	// model environmental variance according to target hsq
	// H2 = VA / VA + VE
	V_G = sd(G)^2;
	V_E = (V_G-hsq*V_G)/hsq;
	env = rnorm(size(inds), 0.0, sqrt(V_E));
	P = G + env;
	// set fitness effects based on stabilizing selection
	inds.fitnessScaling = exp(-(((P - P_O)^2) / ((2)*(w^2))));
	inds.tagF = P;
}

200000 late() {
	catn("NO COALESCENCE BY CYCLE 200000");
}

200000:200400 late() {
	mean_pheno =  mean(p1.individuals.tagF);
	var_pheno = var(p1.individuals.tagF);
	catn(sim.cycle + ": m == " + mean_pheno + ", v == " + var_pheno);
}

200000:200400 late() {
	if(sim.cycle == 200000 | sim.cycle == 200100 | sim.cycle == 200200 | sim.cycle == 200300 | sim.cycle == 200400)
	{
		QTLs = sim.mutationsOfType(m1);
		f = sim.mutationFrequencies(NULL, QTLs);
		s = QTLs.selectionCoeff;
		p = QTLs.position;
		indices = order(f, F);
		lines = NULL;
		for (i in indices){
			mutLine = paste0(p[i], "\t", s[i], "\t", f[i], "\n");
			lines = c(lines, mutLine);
		}
		file = paste0(lines);
		file = "position\tselcoeff\tfreq\n" + file;
		writeFile(path + "QTLs_" + asString(sim.cycle) + "_" + nrun + ".txt", file);
	
		// 100 individuals
		sample = sample(p1.individuals, 100);
		sample_pheno = sample.tagF;
		writeFile(path + "100_tpheno_" + asString(sim.cycle) + "_" + nrun + ".txt", asString(sample_pheno));
		sample.genomes.outputVCF(path + "100_noMultiallelicsVCF_" + asString(sim.cycle) + "_" + nrun + ".vcf", outputMultiallelics = F, simplifyNucleotides = T);
		// 500 individuals
		sample = sample(p1.individuals, 500);
		sample_pheno = sample.tagF;
		writeFile(path + "500_tpheno_" + asString(sim.cycle) + "_" + nrun + ".txt", asString(sample_pheno));
		sample.genomes.outputVCF(path + "500_noMultiallelicsVCF_" + asString(sim.cycle) + "_" + nrun + ".vcf", outputMultiallelics = F, simplifyNucleotides = T);
		// 1000 individuals
		sample = sample(p1.individuals, 1000);
		sample_pheno = sample.tagF;
		writeFile(path + "1000_tpheno_" + asString(sim.cycle) + "_" + nrun + ".txt", asString(sample_pheno));
		sample.genomes.outputVCF(path + "1000_noMultiallelicsVCF_" + asString(sim.cycle) + "_" + nrun + ".vcf", outputMultiallelics = F, simplifyNucleotides = T);
	}
}
